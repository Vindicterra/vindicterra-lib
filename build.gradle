import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'org.vindicterra'
version = '0.1.4-dev'

publishing {
    publications {
        pluginPublication(MavenPublication) {
            artifactId = 'vindicterra-lib'
            from components.java
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/Vindicterra/VindicterraLib")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url 'https://repo.xenondevs.xyz/releases'
    }
    maven {
        url 'https://jitpack.io/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT")
    compileOnly 'org.projectlombok:lombok:1.18.28'
    compileOnly "com.github.LoneDev6:API-ItemsAdder:3.6.3-beta-14"

    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'commons-lang:commons-lang:2.4'
    implementation 'xyz.xenondevs.invui:invui:1.43'

    annotationProcessor 'org.projectlombok:lombok:1.18.28'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn tasks.shadowJar
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
}

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = ''
}